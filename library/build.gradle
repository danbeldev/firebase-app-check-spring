import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.vanniktech.maven.publish' version '0.32.0'
}

group = 'io.github.danbel'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'com.auth0:java-jwt:4.4.0'
    implementation 'com.auth0:jwks-rsa:0.22.1'
}

tasks.named('test') {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.13"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

mavenPublishing {

    coordinates("io.github.danbeldev", "firebase-app-check-spring-boot-starter", "0.0.1")

    pom {
        name.set("firebase-app-check-spring-boot-starter")
        description.set("Spring Boot Starter для интеграции Firebase App Check")
        inceptionYear.set("2025")
        url.set("https://github.com/danbeldev/firebase-app-check-spring")

        licenses {
            license {
                name.set("MIT")
                url.set("https://opensource.org/licenses/MIT")
            }
        }

        developers {
            developer {
                id.set("DanBel")
                name.set("Danila Belyakov")
                email.set("dan.bel.89@bk.ru")
            }
        }

        scm {
            url.set("https://github.com/danbeldev/firebase-app-check-spring")
        }
    }

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    signAllPublications()
}
